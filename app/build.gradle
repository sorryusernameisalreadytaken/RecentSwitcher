plugins {
    id 'com.android.application'
}

android {
    // The namespace remains eu.recentsopener so that existing Java packages
    // continue to compile without refactoring. The applicationId below
    // determines the final package name of the APK (eu.ras).
    namespace 'eu.recentsopener'
    compileSdk 34

    defaultConfig {
        // Publish the app under the eu.ras package id. This does not
        // require renaming the Java packages thanks to the separate
        // namespace defined above.
        applicationId "eu.ras"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            // nix nötig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

/**
 * Fix für "Duplicate class kotlin.*":
 * Irgendeine Dependency zieht kotlin-stdlib 1.8.22 und irgendwo hängt noch 1.6.21 dran.
 * Wir zwingen alle kotlin-stdlib* Artefakte auf 1.8.22.
 */
configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "org.jetbrains.kotlin"
                && details.requested.name != null
                && details.requested.name.startsWith("kotlin-stdlib")) {
            details.useVersion "1.8.22"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.13.1'
}